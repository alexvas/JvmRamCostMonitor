plugins {
    alias(libs.plugins.jlink)
}

application {
    mainModule = 'jvmram.app.backend.dist'
    mainClass  = 'jvmram.dist.Main'
}

dependencies {
    implementation libs.slf4j.api
    runtimeOnly libs.slf4j.simple

    implementation project(':app-backend')
}

jlink {
    imageName = 'jvm-ram-cost-runtime'

    options = [
        '--strip-debug',
        '--no-header-files',
        '--no-man-pages',
        '--compress=0' // и так будет сжат в инсталлере
    ]

    launcher {
        name = 'jvm-ram-cost'
    }
}

jpackage {
    jpackageHome = System.getProperty('java.home')
    
    imageName = 'jvm-ram-cost'
    appVersion = libs.versions.jvmram
    vendor = 'Alexander Vasilev'

    installerType = 'deb' // или dmg, deb, rpm, msi

    installerOptions = [
        '--linux-menu-group', 'Utilities',
        '--linux-shortcut'
    ]
}
