plugins {
    id 'java'
    id 'application'
    alias(libs.plugins.protobuf)
}

dependencies {
    implementation libs.slf4j.api
    runtimeOnly libs.slf4j.simple
    compileOnly libs.jspecify

    implementation platform(libs.grpc.bom)
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-services"
    runtimeOnly "io.grpc:grpc-netty-shaded"
    protobuf files('../proto')

    implementation(project(':model'))
    implementation project(':core')
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${libs.versions.protobuf.get()}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${libs.versions.grpc.get()}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

application {
    mainClass = 'jvmram.backend.JvmRunCost'
}

distributions {
    main {
        contents {
            from('../README.md') {
                into 'docs'
            }
            from ('src/main/resources/simplelogger.properties') {
                into 'conf'
            }
        }
    }
}

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('CLASSPATH=', 'CLASSPATH=$APP_HOME/conf:')
        windowsScript.text = windowsScript.text.replace('CLASSPATH=', 'CLASSPATH=%APP_HOME%\\conf;')
    }
}
